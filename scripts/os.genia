TRUE = 1 == 1
FALSE = 1 == 2


define reduce(f, acc) -> define(list) -> reduce(f, acc, list)
define reduce(_, acc, []) -> acc
define reduce(func, acc, [f, ..r]) -> reduce(func, func(acc, f), r)

define count([]) -> 0
define count(list) -> reduce(define (acc, _) -> acc + 1, 0, list)

define filter(f) -> define(list) -> filter(f, list)
define filter(_, []) -> []
define filter(pred, list) -> reduce(define (acc, el) when pred(el) -> [..acc, el] | (acc, _) -> acc, [], list)
define not(v) when v -> FALSE | (_) -> TRUE

define grep (regex)                 -> (define(list)    -> grep(regex, TRUE, list))
    |   (regex, rtnval)         -> (define(list)    -> grep(regex, rtnval, list))
    |   (regex, rtnval, seq)    ->  
            filter(
                define (el) when el ~ regex     -> rtnval 
                    | (_)                   -> not(rtnval),
                seq)




define rcompose () -> (define(x) -> x) 
    | (f) -> (define(x) -> f(x))
    | (f, g) -> (define(x) -> g(f(x)))
    | (f, g, h) -> (define(x) -> h(g(f(x))))
    | (f, g, h, i) -> (define(x) -> i(h(g(f(x)))))

f = find_files(".")
count(f)
ff = rcompose(
    grep(".*/[.]git/.*", FALSE),
    grep(".*/[.]vscode/.*", FALSE),
    grep(".*/[.].*", FALSE),
    grep(".*/.*[.]genia")
) 
filtered = ff(f)
print(count(filtered))
    

[a,b,c] = f
print(a,b,c)

[a,b,c,d,d2,d3] = filtered
print(a,b,c,d,d2,d3)
print(count(f))
